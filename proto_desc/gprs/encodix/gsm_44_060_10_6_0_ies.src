`INCLUDE 3gpp.src`

csn.1 slave {
-- 3GPP TS 44.060 version 10.6.0 Release 10

-- imported from 44.118 9.3.5
< CN Domain Identity IE > ::=
   < CN Domain Identity : bit (2) > ;
< CN Domain Specific DRX Cycle Length Coefficient IE > ::=
   < CN Domain Specific DRX Cycle Length Coefficient : bit (2) >;

-- 12.3
< Ack/Nack Description IE > ::=
   < FINAL_ACK_INDICATION : bit (1) >
   < STARTING_SEQUENCE_NUMBER : bit (7) >
   < RECEIVED_BLOCK_BITMAP : bit (64) > ;

-- 12.3.1
< EGPRS Ack/Nack Description IE > ::=
     0 < EGPRS Ack/Nack Description struct > -- This IE fills rest of message
     | 1 < Length L : bit (8) >                -- Value part of this IE is of length L
         { < bit (val(Length L)) > & < EGPRS Ack/Nack Description struct > } ;

< EGPRS Ack/Nack Description struct > ::=
     < FINAL_ACK_INDICATION : bit (1) >
     < BEGINNING_OF_WINDOW : bit (1) >
     < END_OF_WINDOW : bit (1) >
     < STARTING_SEQUENCE_NUMBER : bit (11) >
     { 0 | 1 < COMPRESSED_BITMAP_LENGTH: bit (7) >
         < COMPRESSED_BITMAP_STARTING_COLOR_CODE: bit (1) >
         < COMPRESSED_RECEIVED_BLOCK_BITMAP :
             bit (val(COMPRESSED_BITMAP_LENGTH)) > }
     < UNCOMPRESSED_RECEIVED_BLOCK_BITMAP: bit** > ;

-- 12.3.2
< FLO Ack/Nack Description IE > ::=
      < FLO Ack/Nack Description struct > ;
< FLO Ack/Nack Description struct > ::=
      < BEGINNING_OF_WINDOW : bit (1) >
      < END_OF_WINDOW : bit (1) >
      < STARTING_SEQUENCE_NUMBER : bit (10) >
      { 0 < COMPRESSED_BITMAP_LENGTH: bit (7) >
          < COMPRESSED_BITMAP_STARTING_COLOR_CODE: bit (1) >
          < COMPRESSED_RECEIVED_BLOCK_BITMAP: bit (val(COMPRESSED_BITMAP_LENGTH)) >
      | 1 < UNCOMPRESSED_RECEIVED_BLOCK_BITMAP: bit** > } ;

-- 12.5.1
< EGPRS Channel Quality Report IE> ::=
   < EGPRS BEP Link Quality Measurements : < EGPRS BEP Link Quality Measurements IE>>
   < C_VALUE : bit (6) >
   < EGPRS Timeslot Link Quality Measurements : <EGPRS Timeslot Link Quality Measurements IE >> ;

-- 12.5.2
< EGPRS Window Size IE > ::=
  < EGPRS Window Size: bit (5) > ;

-- 12.5.3
<EGPRS BEP Link Quality Measurements IE> ::=
  { 0 | 1 < GMSK_MEAN_BEP : bit (5) >
          < GMSK_CV_BEP : bit (3) >}
  { 0 | 1 < 8PSK_MEAN_BEP : bit (5) >
          < 8PSK_CV_BEP : bit (3) > };

-- 12.5.4
<EGPRS Timeslot Link Quality Measurements IE> ::=
{ 0 | 1< BEP_MEASUREMENTS : BEP Measurement Report Struct >}
{ 0 | 1 < INTERFERENCE_MEASUREMENTS : Interference Measurement Report Struct >};

< BEP Measurement Report Struct > ::=
    { 0 | 1 { 0 <GMSK_MEAN_BEP _TN0 : bit (4) >
            | 1 < 8PSK_MEAN_BEP _TN0 : bit (4) >}}
    { 0 | 1 { 0 <GMSK_MEAN_BEP _TN1 : bit (4) >
            | 1 < 8PSK_MEAN_BEP _TN1 : bit (4) >}}
    { 0 | 1 { 0 <GMSK_MEAN_BEP _TN2 : bit (4) >
            | 1 < 8PSK_MEAN_BEP _TN2 : bit (4) >}}
    { 0 | 1 { 0 <GMSK_MEAN_BEP _TN3 : bit (4) >
            | 1 < 8PSK_MEAN_BEP _TN3 : bit (4) >}}
    { 0 | 1 { 0 <GMSK_MEAN_BEP _TN4 : bit (4) >
            | 1 < 8PSK_MEAN_BEP _TN4 : bit (4) >}}
    { 0 | 1 { 0 <GMSK_MEAN_BEP _TN5 : bit (4) >
            | 1 < 8PSK_MEAN_BEP _TN5 : bit (4) >}}
    { 0 | 1 { 0 <GMSK_MEAN_BEP _TN6 : bit (4) >
            | 1 < 8PSK_MEAN_BEP _TN6 : bit (4) >}}
    { 0 | 1 { 0 <GMSK_MEAN_BEP _TN7 : bit (4) >
            | 1 < 8PSK_MEAN_BEP _TN7 : bit (4) >} };

< Interference Measurement Report Struct > ::=
{ 0 | 1 < I_LEVEL_TN0 : bit (4) > }
{ 0 | 1 < I_LEVEL_TN1 : bit (4) > }
{ 0 | 1 < I_LEVEL_TN2 : bit (4) > }
{ 0 | 1 < I_LEVEL_TN3 : bit (4) > }
{ 0 | 1 < I_LEVEL_TN4 : bit (4) > }
{ 0 | 1 < I_LEVEL_TN5 : bit (4) > }
{ 0 | 1 < I_LEVEL_TN6 : bit (4) > }
{ 0 | 1 < I_LEVEL_TN7 : bit (4) > };

-- 12.5.5
< PDCH Pairs Description IE > ::=
   {0                  -- Single Carrier Assignment
      { 00                 -- Default PDCH pair configuration
      | 01                 -- Unchanged
      | 10                 -- Explicit PDCH pair configuration
          < DOWNLINK_PDCH_PAIRS_C1 : bit (8) >
          < UPLINK_PDCH_PAIRS_C1 : bit (8) >
      ! < PDCH pairs configuration error : { 1 1 } bit (*) = < no string > >
      }
   |1                  -- Dual Carrier Assignment
      { 00                 -- Default PDCH pair configuration
      | 01                 -- Unchanged
      | 10                 -- Explicit PDCH pair configuration
          < DOWNLINK_PDCH_PAIRS_C1 : bit (8) >
          < DOWNLINK_PDCH_PAIRS_C2 : bit (8) >
          < UPLINK_PDCH_PAIRS_C1 : bit (8) >
          < UPLINK_PDCH_PAIRS_C2 : bit (8) >
      ! < PDCH pairs configuration error : { 1 1 } bit (*) = < no string > >
      }
   };

-- 12.5a.1
< EGPRS Channel Quality Report Type 2 IE> ::=
   < EGPRS BEP Link Quality Measurements Type 2 : < EGPRS BEP Link Quality Measurements Type 2 IE>>
   < C_VALUE : bit (6) >
   < EGPRS Timeslot Link Quality Measurements Type 2 : <EGPRS Timeslot Link Quality Measurements Type 2 IE
>> ;

-- 12.5a.2
<EGPRS BEP Link Quality Measurements Type 2 IE> ::=
  { 0 | 1 < GMSK_MEAN_BEP : bit (5) >
          < GMSK_CV_BEP : bit (3) >}
  { 0 | 1 < 8PSK_MEAN_BEP : bit (5) >
          < 8PSK_CV_BEP : bit (3) > }
  { 0 | 1 < QPSK_MEAN_BEP : bit (5) >
          < QPSK_CV_BEP : bit (3) > }
  { 0 | 1 < 16QAM_ NSR_MEAN_BEP : bit (5) >
          < 16QAM_ NSR_CV_BEP : bit (3) > }
  { 0 | 1 < 32QAM_ NSR_MEAN_BEP : bit (5) >
          < 32QAM_ NSR_CV_BEP : bit (3) > }
  { 0 | 1 < 16QAM_HSR_MEAN_BEP : bit (5) >
          < 16QAM_HSR_CV_BEP : bit (3) > }
  { 0 | 1 < 32QAM_HSR_MEAN_BEP : bit (5) >
          < 32QAM_HSR_CV_BEP : bit (3) > };

-- 12.5a.3
<EGPRS Timeslot Link Quality Measurements Type 2 IE> ::=
{ 0 | 1 < BEP_MEASUREMENTS : BEP Measurement Report Struct >}
{ 0 | 1 < INTERFERENCE_MEASUREMENTS : Interference Measurement Report Struct >};

< BEP Measurement Report Struct > ::=
    {0
    | 1 <REPORTED_MODULATION : bit (2) >
         <MEAN_BEP_TN0 : bit (4) >
    }
    {0
    | 1 < REPORTED_MODULATION : bit (2) >
         <MEAN_BEP_TN1 : bit (4) >
    }
    {0
    | 1 < REPORTED_MODULATION : bit (2) >
         <MEAN_BEP_TN2 : bit (4) >
    }
    {0
    | 1 < REPORTED_MODULATION : bit (2) >
         <MEAN_BEP_TN3 : bit (4) >
    }
    {0
    | 1 < REPORTED_MODULATION : bit (2) >
         <MEAN_BEP_TN4 : bit (4) >
    }
    {0
    | 1 < REPORTED_MODULATION : bit (2) >
         <MEAN_BEP_TN5 : bit (4) >
    }
    {0
    | 1 < REPORTED_MODULATION : bit (2) >
         <MEAN_BEP_TN6 : bit (4) >
    }
    {0
    | 1 < REPORTED_MODULATION : bit (2) >
         <MEAN_BEP_TN7 : bit (4) >
    };

< Interference Measurement Report Struct > ::=
{ 0 | 1 < I_LEVEL_TN0 : bit (4) > }
{ 0 | 1 < I_LEVEL_TN1 : bit (4) > }
{ 0 | 1 < I_LEVEL_TN2 : bit (4) > }
{ 0 | 1 < I_LEVEL_TN3 : bit (4) > }
{ 0 | 1 < I_LEVEL_TN4 : bit (4) > }
{ 0 | 1 < I_LEVEL_TN5 : bit (4) > }
{ 0 | 1 < I_LEVEL_TN6 : bit (4) > }
{ 0 | 1 < I_LEVEL_TN7 : bit (4) > };

-- 12.7
< Channel Request Description IE > ::=
   < PEAK_THROUGHPUT_CLASS : bit (4) >
   < RADIO_PRIORITY : bit (2) >
   < RLC_MODE : bit (1) >
   < LLC_ PDU_TYPE : bit (1) >
   < RLC_OCTET_COUNT : bit (16) > ;

-- 12.7a
< Iu mode Channel Request Description IE > ::=
    < RB Id : bit (5) >
    < RADIO_PRIORITY : bit (2) >
    { 0 | 1 < RLC_BLOCK_COUNT : bit (8) > }
    { 0 | 1 < Iu mode Channel Request Description IE > } ; -- IE to be repeated only when
                                                           -- in a Multiple TBF request message

-- 12.7b
< Extended Channel Request Description IE > ::=
   < PFI : bit (7) >
   < RADIO_PRIORITY : bit (2) >
   < RLC_MODE : bit (1) >
   { 0 | 1 < LLC_ PDU_TYPE : bit (1) > }
   { 0 | 1 < Extended Channel Request Description IE > } ; -- IE to be repeated only when needed and
                                                           -- when included in a Multiple TBF request message

-- 12.8
< Frequency Parameters IE > ::=
    < TSC : bit (3) >
    { 00 < ARFCN : bit (10) >
    | 01 < Indirect encoding : < Indirect encoding struct > >
    | 10 < Direct encoding 1 : < Direct encoding 1 struct > >
    | 11 < Direct encoding 2 : < Direct encoding 2 struct > > } ;

< Indirect encoding struct > ::=
    < MAIO : bit (6) >
    < MA_NUMBER : bit (4) >
    { 0 | 1 < CHANGE_MARK_1 : bit (2) >
            { 0 | 1 < CHANGE_MARK_2 : bit (2) > } } ;

< Direct encoding 1 struct > ::=
    < MAIO : bit (6) >
    < GPRS Mobile Allocation : < GPRS Mobile Allocation IE > > ;

< Direct encoding 2 struct > ::=
    < MAIO : bit (6) >
    < HSN : bit (6) >
    < Length of MA Frequency List contents : bit (4) >
    < MA Frequency List contents : octet (val(Length of MA Frequency List contents) + 3) > ;

-- 12.8.2
< Dual Carrier Frequency Parameters IE > ::=
   < TSC : bit (3) >
   { 00
        { 0 | 1 < ARFCN1 : bit (10) >
            < ARFCN2 : bit (10) > }
   | 01 < Indirect encoding : < Dual Carrier Indirect encoding struct > >
   | 10 < Direct encoding 1 : < Dual Carrier Direct encoding 1 struct > >
   | 11 < Direct encoding 2 : < Dual Carrier Direct encoding 2 struct > > } ;

< Dual Carrier Direct encoding 1 struct > ::=
   { 0 | 1 < MAIO1 : bit (6) > }
   { 0 | 1 < MAIO2 : bit (6) > }
   < GPRS Mobile Allocation : < GPRS Mobile Allocation IE > > ;

< Dual Carrier Indirect encoding struct > ::=
   { 0 | 1 < MAIO1 : bit (6) > }
   { 0 | 1 < MAIO2 : bit (6) > }
   < MA_NUMBER : bit (4) >
   { 0 | 1 < CHANGE_MARK_1 : bit (2) >
            { 0 | 1 < CHANGE_MARK_2 : bit (2) > } } ;

< Dual Carrier Direct encoding 2 struct > ::=
   { 0 | 1 < MAIO1 : bit (6) > }
   { 0 | 1 < MAIO2 : bit (6) > }
   < HSN : bit (6) >
   < Length of MA Frequency List contents : bit (4) >
   < MA Frequency List contents : octet (val(Length of MA Frequency List contents) + 3) > ;

-- 12.8.3
< Pulse Format IE > ::=
   { 0 < Pulse Format Coding 1 : bit (3) >
   | 1 < Pulse Format Coding 2 : < Pulse Format Coding 2 struct > > };

< Pulse Format Coding 2 struct > ::=
   { 0 < Pulse Format Bitmap Length: bit (7) >
       < Pulse Format Bitmap:
          bit (val (Pulse Format Bitmap Length) + 1)
          & { { 1 | 01 | 00 } ** ! { bit** = <no string> } } >
   | 1 < Non Hopping Carrier Pulse Format : 1 | 01 | 00 >
   };

-- 12.9
< Global Power Control Parameters IE > ::=
   < ALPHA : bit (4) >
   < T_AVG_W : bit (5) >
   < T_AVG_T : bit (5) >
   < Pb : bit (4) >
   < PC_MEAS_CHAN : bit (1) >
   0                 -- The value '1' was allocated in an earlier version of the protocol and shall not be used.
   < N_AVG_I : bit (4) > ;

-- 12.9a
< GPRS Power Control Parameters IE > ::=
   < ALPHA : bit (4) >
   < T_AVG_W : bit (5) >
   < T_AVG_T : bit (5) >
   < PC_MEAS_CHAN : bit >
   < N_AVG_I : bit (4) > ;

-- 12.10
< Global TFI IE > ::=
   { 0 < UPLINK_TFI : bit (5) >
   | 1 < DOWNLINK_TFI : bit (5) > } ;

-- 12.10a
< GPRS Mobile Allocation IE > ::=
   < HSN : bit (6) >
   { 0 | 1 < RFL number list : < RFL number list struct > > }
   { 0 < MA_LENGTH : bit (6) >
        < MA_BITMAP : bit (val(MA_LENGTH) + 1) >
   | 1 { 0 | 1 < ARFCN index list : < ARFCN index list struct > > } } ;

< RFL number list struct > ::=
   < RFL_NUMBER : bit (4) >
   { 0 | 1 < RFL number list struct > } ;

< ARFCN index list struct > ::=
   < ARFCN_INDEX : bit (6) >
   { 0 | 1 < ARFCN index list struct > } ;

-- 12.10d
< EGPRS Modulation and Coding Scheme IE > ::=
   bit(4) ;

-- 12.10f
< EGPRS Level IE > ::=
   bit(2) ;

-- 12.11
< Packet Request Reference IE > ::=
   < RANDOM_ACCESS_INFORMATION value : bit (11) >
   < FRAME_NUMBER : bit (16) > ;

-- 12.12
< Packet Timing Advance IE > ::=
   { 0 | 1 < TIMING_ADVANCE_VALUE : bit (6) > }
   { 0 | 1 < TIMING_ADVANCE_INDEX : bit (4) >
           < TIMING_ADVANCE_TIMESLOT_NUMBER : bit (3) > } ;

-- 12.12a
< Global Packet Timing Advance IE > ::=
          { 0 | 1 < TIMING_ADVANCE_VALUE : bit (6) > }
          { 0 | 1 < UPLINK_TIMING_ADVANCE_INDEX : bit (4) >
                   < UPLINK_TIMING_ADVANCE_TIMESLOT_NUMBER : bit (3) > }
          { 0 | 1 < DOWNLINK_TIMING_ADVANCE_INDEX : bit (4) >
                   < DOWNLINK_TIMING_ADVANCE_TIMESLOT_NUMBER : bit (3) > } ;

-- 12.13
< Power Control Parameters IE > ::=
   < ALPHA : bit (4) >
   { 0 | 1 < GAMMA_TN0 : bit (5) > }
   { 0 | 1 < GAMMA_TN1 : bit (5) > }
   { 0 | 1 < GAMMA_TN2 : bit (5) > }
   { 0 | 1 < GAMMA_TN3 : bit (5) > }
   { 0 | 1 < GAMMA_TN4 : bit (5) > }
   { 0 | 1 < GAMMA_TN5 : bit (5) > }
   { 0 | 1 < GAMMA_TN6 : bit (5) > }
   { 0 | 1 < GAMMA_TN7 : bit (5) > } ;

-- 12.14
< PRACH Control Parameters IE > ::=
   < ACC_CONTR_CLASS : bit (16) >
   < MAX_RETRANS : bit (2) > * 4
   < S : bit (4) >
   < TX_INT : bit (4) >
   { 0 | 1 < PERSISTENCE_LEVEL : bit (4) > * 4 } ;

-- 12.16
< TLLI / G-RNTI IE > ::=
   < TLLI / G-RNTI : bit(32) > ;

< TLLI IE > ::=
   < TLLI : bit(32) > ;

-- 12.16a
< G-RNTI IE > ::=
   < S-RNTI : bit (20) >
   < Serving BSC Identity : bit (12) > ;

-- 12.21
< Starting Frame Number Description IE > ::=
   { 0 < Absolute Frame Number Encoding : bit(16) >
   | 1 < Relative Frame Number Encoding : bit(13) > } ;

-- 12.23
< Cell Identification IE > ::=
   < Location Area Identification IE : octet (5) > -- 3GPP TS 44.018
   < RAC : bit (8) >
   < Cell Identity IE : octet (2) > ;              -- 3GPP TS 44.018

-- 12.24
< GPRS Cell Options IE > ::=
   < NMO : bit (2) >
   < T3168 : bit (3) >
   < T3192 : bit (3) >
   < DRX_TIMER_MAX : bit (3) >
   < ACCESS_BURST_TYPE : bit >
   < CONTROL_ACK_TYPE : bit >
   < BS_CV_MAX : bit (4) >
   { 0 | 1 < PAN_DEC : bit (3) >
            < PAN_INC : bit (3) >
            < PAN_MAX : bit (3) > }
   -- Optional extension information:
     { 0 | 1 < Extension Length : bit (6) >
	      { < bit (val(Extension Length) + 1) >
	        & <Extension Information > } ! { bit ** = <no string> } } ;

< Extension Information > ::=
   { { -- R99 extension:
        { 0 | 1 -- EGPRS supported by the cell if the choice bit is set to '1'
                 < EGPRS_PACKET_CHANNEL_REQUEST : bit >
                 < BEP_PERIOD : bit (4) > }
        < PFC_FEATURE_MODE: bit >
        < DTM_SUPPORT: bit >
        < BSS_PAGING_COORDINATION: bit > }
   { -- REL-4 extension:
        < CCN_ACTIVE : bit >
        < NW_EXT_UTBF : bit > }
   { -- REL 6 extension:
        < MULTIPLE_TBF_CAPABILITY : bit >
        < EXT_UTBF_NODATA : bit >
        < DTM_ENHANCEMENTS_CAPABILITY : bit >
        {0          -- MBMS procedures not supported by the cell if the choice bit is set to '0'
        |1          -- MBMS procedures supported by the cell if the choice bit is set to '1'
            < DEDICATED_MODE_MBMS_NOTIFICATION_SUPPORT: bit >
            < MNCI_SUPPORT : bit > } }
   { -- Rel-7 extension:
        < REDUCED_LATENCY_ACCESS : bit > }
   < spare bit > ** } // ;        -- Extension information may be truncated between released versions of the protocol.
                                  -- The receiver shall assume the value zero for any truncated bit.

-- 12.25
< PCCCH Organization Parameters IE > ::=
   < BS_PCC_REL : bit >
   < BS_PBCCH_BLKS : bit (2) >
   < BS_PAG_BLKS_RES : bit (4) >
   < BS_PRACH_BLKS : bit (4) > ;

-- 12.26
< Extension Bits IE > ::=
   < extension length : bit (6) >
   < spare bit (val(extension length)+1) > ;

-- 12.27
< Non GPRS Cell Options IE > ::=
   < ATT : bit >                                  -- Attach/Detach allowed
   { 0 | 1 < T3212 : bit (8) > }                  -- Time-out value for periodic update
   < NECI : bit >                                 -- Half rate support
   < PWRC : bit >                                 -- Power Control indicator
   < DTX : bit (2) >                              -- DTX indicator
   < RADIO-LINK-TIMEOUT : bit (4) >               -- Supervisory timer for RR connection
   < BS-AG-BLKS-RES : bit (3) >                   -- number of blocks reserved for access grant
   < CCCH-CONF : bit (3) >                        -- physical channel configuration for CCCH
   < BS-PA-MFRMS : bit (3) >                      -- number of 51 multiframes between
                                                  -- transmission of paging messages
   < MAX-RETRANS : bit (2) >                      -- maximum number of retransmissions
   < TX-INTEGER : bit (4) >                       -- number of slots to spread transmission
   < EC : bit >                                   -- emergency call allowed
   < MS-TXPWR-MAX-CCCH : bit (5) >                -- maximum Tx power level
   -- Optional extension information:
   { 0 | 1 < Extension Length : bit (6) >
            { < bit (val(Extension Length) + 1) >
	      & <Extension Information > } ! { bit ** = <no string> } } ;
< Extension Information > ::=
   < ECSC: bit >                                  -- Early Classmark Sending Control
   < 3G ECSR : bit >                                    -- 3G Early Classmark Sending Restriction
   < spare bit > ** ;

-- 12.28
< LSA Parameters IE > ::=
   < NR_OF_FREQ_OR_CELLS : bit (5) >
   { < LSA ID information : < LSA ID information struct >> * (val(NR_OF_FREQ_OR_CELLS)) };

< LSA ID information struct > ::=
   { 1 { 0 < LSA_ID : bit (24) >
       |1 < ShortLSA_ID : bit (10) >} } ** 0 ;

-- 12.29
< COMPACT reduced MA IE > ::=
   <Length of Reduced MA bitmap : bit (7) >
   <Reduced MA bitmap : bit( val( Length of Reduced MA bitmap ) ) >
   { 0 | 1 <MAIO_2 : bit(6) >};

-- 12.30
< MS Radio Access Capability 2 IE > ::=
   < MS RA capability : < MS RA capability value part struct > > ;

	-- From 24.008
	slave <MS RA capability value part struct >::= --recursive structure allows any number of Access technologies
		< Access Technology Type: bit (4) >
		< Access capabilities : <Access capabilities struct> >
		{ 0 | 1 <MS RA capability value part struct> } ;

	slave < Access capabilities struct > ::=
		< Length : bit (7) > -- length in bits of Content and spare bits
		ENCODE: %{PUSH_TAG (2, CURPOS);}%
		DECODE: %{PUSH_TAG (1, (EDBitsToInt (BUFFER, CURPOS-7, 7)+CURPOS));}% 
		<Access capabilities : <Content>>
		<spare bits>*ENCODE_EXPR: %{0}% DECODE_EXPR: %{(TAG(1)>(int)CURPOS? TAG(1)-CURPOS:(CURPOS=TAG(1),0))}% -- expands to the indicated length
		ENCODE: %{EDIntToBits (BUFFER, TAG(2)-7, CURPOS-TAG(2), 7);}% 
		DECODE: %{}%;
	  -- may be used for future enhancements

	slave < Content > ::=
		< RF Power Capability : bit (3) >
		{ 0 | 1 <A5 bits : <A5 bits> > } -- zero means that the same values apply for parameters as in the immediately preceeding Access capabilities field within this IE
		-- The presence of the A5 bits is mandatory in the 1st Access capabilities struct within this IE.
		< ES IND : bit >
		< PS : bit >
		< VGCS : bit >
		< VBS : bit >
		{ 0 | 1 < Multislot capability : Multislot capability struct > } -- zero means that the same values for multislot parameters as given in an earlier Access capabilities field within this IE apply also here
		-- Additions in release 99
		{ 0 | 1 < m8PSK Power Capability : bit(2) >} -- 1 also means 8PSK modulation capability in uplink.
		< COMPACT Interference Measurement Capability : bit >
		< Revision Level Indicator : bit >
		< UMTS FDD Radio Access Technology Capability : bit > -- 3G RAT
		< UMTS 3.84 Mcps TDD Radio Access Technology Capability : bit > -- 3G RAT
		< CDMA 2000 Radio Access Technology Capability : bit > -- 3G RAT
		< UMTS 1.28 Mcps TDD Radio Access Technology Capability: bit > -- 3G RAT
		< MS_EXT_UTBF : bit >;
		-- error: struct too short, assume features do not exist
		-- error: struct too long, ignore data and jump to next Access technology

	slave < Multislot capability struct > ::=
		{ 0 | 1 < HSCSD multislot class : bit (5) > }
		{ 0 | 1 < GPRS multislot class : bit (5) > < GPRS Extended Dynamic Allocation Capability : bit > }
		{ 0 | 1 < SMS_VALUE : bit (4) > < SM_VALUE : bit (4) > }
			-- Additions in release 99
		{ 0 | 1 < ECSD multislot class : bit (5) > }
		{ 0 | 1 < EGPRS multislot class : bit (5) > < EGPRS Extended Dynamic Allocation Capability : bit > }
		{0 | 1 < DTM GPRS Multi Slot Sub-Class: bit(2)>
				<MAC Mode Support : bit>
				{0 | 1 <DTM EGPRS Multi Slot Sub-Class : bit(2)> }
		};
			-- error: struct too short, assume features do not exist

-- 12.31
< UTRAN FDD Target cell IE > ::=
   < FDD-ARFCN : bit (14) >
   < Diversity : bit >
   { 0 | 1 < Bandwidth_FDD : bit (3) > }
   < SCRAMBLING_CODE : bit (9) > ;

-- 12.32
< UTRAN TDD Target cell IE > ::=
   < TDD-ARFCN : bit (14) >
   < Diversity TDD : bit >
   { 0 | 1 < Bandwidth_TDD : bit (3) > }
   < Cell Parameter : bit (7) >
   < Sync Case TSTD : bit > ;

-- 12.33
< TMGI IE > ::=
   {0                                -- without MCC and MNC parameters
      < MBMS SERVICE ID : bit (24) >
   |1                                -- with MCC and MNC parameters
      < MBMS SERVICE ID : bit (24) >
      < MCC : bit (12) >
      < MNC : bit (12) > } ;

-- 12.36
< MBMS Channel Parameters IE >::=
   {0                              -- counting is off
      { 0 | 1 < MBMS p-t-m channel description : < MBMS p-t-m channel description IE > >
              < MBMS Session Parameters List : < MBMS Session Parameters List IE >> }
   |1                              -- counting is on
      { 0 | 1 < MPRACH description : < MPRACH description IE > > } };

-- 12.37
< MBMS p-t-m channel description IE > ::=
   { 0 | 1 < Frequency Parameters : < Frequency Parameters IE > > }
   < DL_TIMESLOT_ALLOCATION : bit (8) >;

-- 12.38
< MPRACH description IE > ::=
   { 0 | 1 < Frequency Parameters : < Frequency Parameters IE > > }
   < MPRACH_TIMESLOT NUMBER : bit (3) >
   < USF : bit (3) >
   {0|1        < MPRACH Control Parameters : < MPRACH Control Parameters IE > > } ;

-- 12.39
< MBMS Sessions List IE > ::=
   { 1 < TMGI : < TMGI IE >>
       { 0 | 1 < MBMS Session Identity : bit (8) > } } ** 0;

-- 12.40
< MBMS Session Parameters List IE > ::=
   { 1 < Length of MBMS Bearer Identity : bit (3) > -- Configurations "000", "110" and "111" are reserved
       < MBMS Bearer Identity : bit (val (Length of MBMS Bearer Identity)) >
       < Estimated Session Duration : bit (8) >
       { 0 | 1 < MBMS Radio Bearer Starting Time : bit (16) > }
       { 0 | 1 < EGPRS Window Size : < EGPRS Window Size IE >> }
       { 0 | 1 < NPM Transfer Time : bit (5) > } } ** 0;

-- 12.41
< MPRACH Control Parameters IE > ::=
   { 0 | 1< ACC_CONTR_CLASS : bit (16) > }
   { 0 | 1< MAX_RETRANS : bit (2) > }
   < S : bit (4) >
   { 0 | 1 < TX_INT : bit (4) > }
   { 0 | 1 < PERSISTENCE_LEVEL : bit (4) > } ;

-- 12.43
< NAS Container for PS Handover IE > ::=
   < NAS_CONTAINER_LENGTH : bit (7) >
< NAS_CONTAINER_DATA : octet (val(NAS_CONTAINER_LENGTH)) >
< padding bits > ;

-- 12.45
< MBMS In-band Signalling Indicator IE > ::=
   < MBMS In-band Signalling Indicator: bit (1) >;

-- 12.45b
< RRC Container IE > ::=
   < RRC_CONTAINER_LENGTH : bit (8) >
   < RRC_CONTAINER_DATA : octet (val(RRC_CONTAINER_LENGTH)) >
   < padding bits > ;

-- 12.47
< CS Handover Radio Resources IE > ::=
   < CS_HANDOVER_RADIO_RESOURCES_LENGTH : bit (7) >
   < CS_HANDOVER_RADIO_RESOURCES_DATA : octet (val(CS_HANDOVER_RADIO_RESOURCES_LENGTH)) >
   < padding bits > ;

-- 12.49
< E-UTRAN Target cell IE > ::=
   < EARFCN : bit (16) >
   { 0 | 1 < Measurement Bandwidth: bit (3) > }
   < Physical Layer Cell Identity : bit (9) > ;

-- 12.50
< Individual priorities IE > ::=
    {0|          -- delete all stored individual priorities
      1          -- provide individual priorities
         < GERAN_PRIORITY : bit(3) >
         { 0 | 1 < 3G Individual Priority Parameters Description :
                 < 3G Individual Priority Parameters Description struct >> }
         { 0 | 1 < E-UTRAN Individual Priority Parameters Description :
                 < E-UTRAN Individual Priority Parameters Description struct >> }
         { 0 | 1 < T3230 timeout value : bit (3) > }
};

< 3G Individual Priority Parameters Description struct > ::=
    { 0 | 1 < DEFAULT_UTRAN_PRIORITY : bit(3) > }
    { 1 < Repeated Individual UTRAN Priority Parameters :
         < Repeated Individual UTRAN Priority Parameters struct >> } ** 0 ;

< Repeated Individual UTRAN Priority Parameters struct > ::=
    { 0 { 1 < FDD-ARFCN : bit (14) > } ** 0 | 1 { 1 < TDD-ARFCN : bit (14) > } ** 0 }
    < UTRAN_PRIORITY : bit(3) > ;

< E-UTRAN Individual Priority Parameters Description struct > ::=
    { 0 | 1 < DEFAULT_E-UTRAN_PRIORITY : bit(3) > }
    { 1 < Repeated Individual E-UTRAN Priority Parameters :
         < Repeated Individual E-UTRAN Priority Parameters struct >> } ** 0 ;

< Repeated Individual E-UTRAN Priority Parameters struct > ::=
    { 1 < EARFCN : bit (16) > } ** 0
    < E-UTRAN_PRIORITY : bit(3) > ;

-- 12.51
< GSM Priority Parameters IE > ::=
   < GERAN_PRIORITY : bit(3) >
   < THRESH_Priority_Search : bit(4) >
   < THRESH_GSM_low : bit(4) >
   < H_PRIO : bit(2) >
   < T_Reselection : bit(2) >;

-- 12.52
< 3G Priority Parameters IE > ::=
   { 0 | 1 < DEFAULT_UTRAN_PRIORITY : bit(3) >
           < DEFAULT_THRESH_UTRAN: bit(5) >
           < DEFAULT_UTRAN_QRXLEVMIN: bit(5) > }
   { 1 < Repeated UTRAN Priority Parameters : < Repeated UTRAN Priority Parameters struct >> } ** 0 ;

< Repeated UTRAN Priority Parameters struct > ::=
   { 1 < UTRAN_FREQUENCY_INDEX : bit (5) > } ** 0
   { 0 | 1 < UTRAN_PRIORITY : bit(3) > }
   < THRESH_UTRAN_high: bit(5) >
   { 0 | 1 < THRESH_UTRAN_low: bit(5) > }
   { 0 | 1 < UTRAN_QRXLEVMIN: bit(5) > } ;

-- 12.53
< E-UTRAN Parameters IE > ::=
   < E-UTRAN_CCN_ACTIVE : bit >
   { 0 | 1 < GPRS E-UTRAN Measurement Parameters :
            < GPRS E-UTRAN Measurement Parameters struct >> }
   { 1 < Repeated E-UTRAN Neighbour Cells : < Repeated E-UTRAN Neighbour Cells struct >> } ** 0
   { 1 < Repeated E-UTRAN Not Allowed Cells : < Repeated E-UTRAN Not Allowed Cells struct >> } ** 0
   { 1 < Repeated E-UTRAN PCID to TA mapping : < Repeated E-UTRAN PCID to TA mapping struct >> } ** 0 ;

< GPRS E-UTRAN Measurement Parameters struct > ::=
   < Qsearch_P_E-UTRAN : bit(4) >
   < E-UTRAN_REP_QUANT : bit >
   < E-UTRAN_MULTIRAT_REPORTING : bit(2) >
   { 0 | 1 < E-UTRAN_FDD_REPORTING_THRESHOLD : bit(3) >
            { 0 | 1 < E-UTRAN_FDD_REPORTING_THRESHOLD_2 : bit(6) > }
            { 0 | 1 < E-UTRAN_FDD_REPORTING_OFFSET : bit(3) > } }
   { 0 | 1 < E-UTRAN_TDD_REPORTING_THRESHOLD : bit(3) >
            { 0 | 1 < E-UTRAN_TDD_REPORTING_THRESHOLD_2 : bit(6) > }
            { 0 | 1 < E-UTRAN_TDD_REPORTING_OFFSET : bit(3) > } } ;

< Repeated E-UTRAN Neighbour Cells struct > ::=
   { 1 < EARFCN : bit (16) >
        { 0 | 1 < Measurement Bandwidth : bit (3) > } } ** 0
   { 0 | 1 < E-UTRAN_PRIORITY : bit(3) > }
   < THRESH_E-UTRAN_high : bit(5) >
   { 0 | 1 < THRESH_E-UTRAN_low : bit(5) > }
   { 0 | 1 < E-UTRAN_QRXLEVMIN : bit(5) > } ;

< Repeated E-UTRAN Not Allowed Cells struct > ::=
   < Not Allowed Cells : < PCID Group IE >>
   { 1 < E-UTRAN_FREQUENCY_INDEX : bit(3) > } ** 0 ;

< Repeated E-UTRAN PCID to TA mapping struct > ::=
   {1                                                        -- each repetition corresponds to a different TA
        < PCID to TA mapping : < PCID Group IE >>            -- group of PCIDs belonging to the same TA
   } ** 0
   { 1 < E-UTRAN_FREQUENCY_INDEX : bit(3) > } ** 0 ;

-- 12.54
< 3G CSG Description IE > ::=
   { 1 < CSG_PSC_SPLIT : < PSC Group IE >>
          { 1 < UTRAN_FREQUENCY_INDEX : bit (5) > } ** 0 } ** 0 ;

-- 12.55
< E-UTRAN CSG Description IE > ::=
   { 1 < CSG_PCI_SPLIT : < PCID Group IE >>
          { 1 < E-UTRAN_FREQUENCY_INDEX : bit (3) > } ** 0 } ** 0 ;

-- 12.56
< Measurement Control Parameters Description IE> ::=
   { 0 | 1 < Measurement_Control_E-UTRAN : bit(1) >
           < E-UTRAN_FREQUENCY_INDEX : bit (3) >
           { 1 < E-UTRAN_FREQUENCY_INDEX : bit (3) > } ** 0
   }
   { 0 | 1 < Measurement_Control_UTRAN : bit(1) >
           < UTRAN_FREQUENCY_INDEX : bit (5) >
           { 1 < UTRAN_FREQUENCY_INDEX : bit (5) > } ** 0
   };

-- 12.57
< PCID Group IE > ::=
   { 1 < PCID : bit (9) > } ** 0 -- explicit PCID coding
   { 0 | 1 < PCID_BITMAP_GROUP : bit (6) > }
   { 1 < PCID_Pattern_length : bit (3) >
        < PCID_Pattern : bit (val(PCID_Pattern_length) + 1) >
        < PCID_pattern_sense : bit (1) >
   } ** 0 ;

-- 12.58
< PSC Group IE > ::=
   { 1 < PSC : bit (9) > } ** 0           -- explicit PSC coding
   { 1 < PSC_Pattern_length : bit (3) >
        < PSC_Pattern : bit (val(PSC_Pattern_length) + 1) >
        < PSC_pattern_sense : bit (1) >
   } ** 0 ;

-- 12.59
< Enhanced Cell Reselection Parameters IE > ::=
   { 1 < Repeated E-UTRAN Enhanced Cell Reselection Parameters :
       < Repeated E-UTRAN Enhanced Cell Reselection Parameters struct >> } ** 0 ;

< Repeated E-UTRAN Enhanced Cell Reselection Parameters struct > ::=
   { 1 < E-UTRAN_FREQUENCY_INDEX : bit (3) > } ** 0
   {0                               -- reselection based on target cell RSRP (RSRQ checked for suitability)
       < E-UTRAN_Qmin: bit(4) >
   |1                               -- reselection based on target cell RSRQ (RSRP checked for suitability)
       < THRESH_E-UTRAN_high_Q: bit(5) >
       { 0 | 1 < THRESH_E-UTRAN_low_Q: bit(5) > }
       { 0 | 1 < E-UTRAN_QQUALMIN : bit(4) > }
       { 0 | 1 < E-UTRAN_RSRPmin : bit(5) > }
   };

-- 12.60
< E-UTRAN CSG Measurement Report IE > ::=
   < E-UTRAN CI : bit (28) >
   < Tracking Area Code : bit (16) >
   { 0 | 1 < PLMN-ID : < PLMN-ID struct > > }
   < CSG-ID : bit (27) >
   < Access Mode : bit (1) >
   < REPORTING_QUANTITY : bit (6) > ;

< PLMN-ID struct > ::=
   < MCC : bit (12) >
   < MNC : bit (12) > ;

-- 12.61
< UTRAN CSG Measurement Report IE > ::=
   < UTRAN CGI : bit (28) >
   { 0 | 1 < PLMN-ID : < PLMN-ID struct > > }
   < CSG-ID : bit (27) >
   < Access Mode : bit (1) >
   < REPORTING_QUANTITY : bit (6) > ;

< PLMN-ID struct > ::=
   < MCC : bit (12) >
   < MNC : bit (12) > ;

-- 12.62
< E-UTRAN CSG Target cell IE > ::=
      <E- UTRAN CI : bit (28) >
      < Tracking Area Code : bit (16) >
      { 0 | 1 < PLMN-ID : < PLMN-ID struct > > } ;

< PLMN-ID struct > ::=
   < MCC : bit (12) >
   < MNC : bit (12) > ;

-- 12.63
< UTRAN CSG Target cell IE > ::=
      < UTRAN CI : bit (28) >
      { 0 | 1 < PLMN-ID : < PLMN-ID struct > > } ;

< PLMN-ID struct > ::=
   < MCC : bit (12) >
   < MNC : bit (12) > ;

}
